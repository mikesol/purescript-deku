module DOM.Indexed where

import Prelude

import Comment (commentModule)
import Control.Monad.Except (ExceptT(..))
import DOM.Common (Ctor, TagNS(..), namespaceBases, webElements)
import DOM.Indexed.Common (requires)
import DOM.Indexed.Elements as Elements
import DOM.Indexed.Interfaces as Interfaces
import DOM.Indexed.Props as Props
import DOM.Indexed.Self as Self
import DOM.Indexed.Values as Values
import DOM.Parse (Specification)
import DOM.TypeStub (typeImports, TypeStub(..))
import Data.Array as Array
import Data.Maybe (Maybe(..))
import Data.Tuple.Nested (type (/\))
import Effect.Aff (Aff, Error, attempt)
import FS as FS
import Node.Encoding (Encoding(..))
import Node.FS.Aff (writeTextFile)
import Partial.Unsafe (unsafePartial)
import PureScript.CST.Types (Export, ImportDecl, Module)
import Tidy.Codegen (declImport, declImportAs, exportClass, exportModule, importType, importValue, module_, printModule)

generateSpec :: String -> String -> Array ( ImportDecl Void ) -> Array ( Export Void ) -> Specification -> ExceptT Error Aff Unit
generateSpec path mod imports exports { keywords, elements, interfaces } = do
    let
        uniqueValues :: Array Values.Keyword
        uniqueValues =
            Array.nub $ map (\{ ctor, value } -> { ctor, value } ) keywords

        attributes :: Array ( Ctor /\ Array Props.AttributeType )
        attributes =
            Props.coalesceAttributes keywords interfaces

    ExceptT $ attempt
        $ writeTextFile UTF8 path
        $ printModule
        $ unsafePartial
        $ warnCodegen
        $ module_ mod
            ( Array.concat
                [ Interfaces.exports interfaces
                , Elements.exports elements
                , Props.exports attributes
                , Values.exports uniqueValues
                , pure $ exportClass "TagToDeku"
                , exports
                ]
            )
            ( requires <> imports )
            ( Array.concat 
                [ Interfaces.generate interfaces
                , Elements.generate elements
                , Props.generate attributes
                , Values.generate uniqueValues
                ]
            )

warnCodegen :: forall a . Module a -> Module a
warnCodegen =
    commentModule
        [ "This module has been automatically generated by running `spago run -p codegen`."
        , "Any changes may be overwritten."
        ]

generate :: Specification -> Specification -> Specification -> ExceptT Error Aff Unit
generate html svg mathml = do
    FS.createDir "deku-dom/src/Deku/DOM"

    ExceptT $ attempt
        $ writeTextFile UTF8  "./deku-dom/src/Deku/DOM/Self.purs"
        $ printModule
        $ unsafePartial
        $ warnCodegen
        $ module_ "Deku.DOM.Self" []
            ( Self.imports webElements )
            ( Self.generate webElements )

    generateSpec "./deku-dom/src/Deku/DOM.purs" "Deku.DOM"
        ( typeImports [ TypeString, TypeKeyword "" ]
            <> [ unsafePartial $ declImportAs "Deku.Control" [ importValue "text", importValue "text_" ] "Deku.Control" ]
        )
        [ unsafePartial $ exportModule "Deku.Control"
        , unsafePartial $ exportModule "Types"
        ]
        html

    let
        globalImport :: Array String -> ImportDecl Void
        globalImport ctors =
            unsafePartial $ declImport "Deku.DOM" $ map importType ctors

        svgDeps :: String -> Maybe String
        svgDeps "SvgGlobal" = Nothing
        svgDeps ctor = Just ctor

    generateSpec "./deku-dom/src/Deku/DOM/SVG.purs" "Deku.DOM.SVG"
        ( Array.cons ( globalImport $ Array.mapMaybe svgDeps $ namespaceBases SVG )
            $ typeImports [ TypeString, TypeKeyword "" ] 
        )
        [ unsafePartial $ exportModule "Types" ]
        svg

    generateSpec "./deku-dom/src/Deku/DOM/MathML.purs" "Deku.DOM.MathML"
        ( Array.cons ( globalImport $ namespaceBases MathML )
            $ typeImports [ TypeString ]
        )
        [ unsafePartial $ exportModule "Types" ]
        mathml